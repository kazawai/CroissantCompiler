?start: statement* //-> interpret

    ?statement: assignment DELIMITER
        | condition
        | loop
        | expression DELIMITER
        | display DELIMITER

    ?assignment: TYPE IDENTIFIER "=" expression -> initialization
        | declaration 
        | IDENTIFIER "=" expression -> modification

    ?declaration: TYPE IDENTIFIER -> declaration

    ?expression: comparison
        | arithmetic_op
        | string_e
        | boolean_e
        | list_e
        | variable

    ?block: "{" statement* "}" -> block

    ?condition: "si" (boolean_e | comparison) "alors" block -> cond_if
        | "si" (boolean_e | comparison) "alors" block "sinon" block -> cond_if_else

    ?loop: "tant" "que" (boolean_e | comparison) "faire" block -> while
        | "pour" "chaque" declaration "dans" (string_e|list_e) "faire" block -> for

    ?comparison: expression ("vaut" | "==") expression -> equals
        | expression ("ne vaut pas" | "!=") expression -> not_equal
        | expression "<=" expression -> less_equal
        | expression ">=" expression -> greater_equal
        | expression "<" expression -> less
        | expression ">" expression -> greater

    ?arithmetic_op: INTEGER_VALUE -> int_atomic_value
        | "-" expression -> int_negation
        | expression "+" expression -> addition
        | expression "-" expression -> subtraction
        | expression "*" expression -> multiplication
        | expression "/" expression -> division

    ?boolean_e: BOOLEAN_VALUE -> bool_atomic_value
        | "non" expression -> bool_negation
        | expression "et" expression -> conjunction
        | expression "ou" expression -> disjunction

    ?string_e: TEXT_VALUE -> string_atomic_value

    ?list_e:  "[]" -> list_bc //basis case
        | "[" sequence "]" -> list_gc //general case
        | "[" expression ":" expression "]" -> range_list
        | expression "["arithmetic_op"]" -> index_list
        | "taille" expression -> size_list
        | "ajouter" expression "dans" expression -> add

    ?sequence: expression ("," expression)*

    ?variable: IDENTIFIER -> call

    ?display: PRINT sequence -> print


// Tokens (type has a priority over identifier)
TYPE.1: /entier|texte|liste|booléen/

PRINT: "afficher"

IDENTIFIER: /[_a-zA-Zé][\wé_]*/
TEXT_VALUE.1: /"[^"]*"/
INTEGER_VALUE.1: /(0)|((-)?[1-9][0-9]*)/
BOOLEAN_VALUE.1: /faux|vrai/

DELIMITER: ";"
// Comments are like python comments
COMMENT: /#[^\n]*/

// Ignore spaces, tabs, newlines and comments
%ignore /[\t \f]+/  // WS
%ignore /\\[\t \f]*\r?\n/   // LINE_JOIN
%ignore /\s+/
%ignore COMMENT

